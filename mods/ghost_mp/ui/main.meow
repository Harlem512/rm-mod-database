let menu = instance_find(omenu_new)
if !instance_exists(menu) { return }

-- dont render unless we're on the main menu or on the rm mp menu
if menu.state != 14 and menu.state != -513 and menu.state != -514 { return }

let lobby = global.steam_lobby_get_lobby_id()

-- MARK: Main Menu
if menu.state == 14 {
  if global.component.button(
    418, 26, 22, 24
  ) {
    menu.state = -513
  }
  draw_sprite_ext(
    seditor_icon_player, 0,
    429, 38,
    1, 1, 0,
    if lobby { c_white } else { c_gray }, 1
  )
  return
}

-- MARK: Emote Rebind
if menu.state == -514 {
  -- check if a key is pressed
  if keyboard_check_pressed(vk_anykey) {
    let key = keyboard_lastkey_get()
    global.rm_mp.emote_button = key
    ini_open("mods/rmml/ghost_mp/config.ini")
    ini_write_real("ghost_mp", "emote_button", key)
    ini_close()
    menu.state = -513
  }
  draw_sprite_stretched(sui_9slice, 0, 54, 34, 336, 118)
  scribble("\n    Press any key to\n  rebind the emote wheel")
      .blend(0, 1)
      .transform(2, 2, 0)
      .draw(60, 39)
  return
}

-- MARK: MP Menu
let col = draw_get_color()
draw_set_color(c_black)

-- back button
if global.component.button(
  418, 2, 22, 22,
) {
  menu.state = 14
}
draw_sprite_ext(
  smenu_bar, 5, 429, 13,
  1, 1, 0, c_black, 1
)

if lobby {
  let count = global.steam_lobby_get_member_count()

  draw_sprite_stretched(sui_9slice, 0, 2, 26, 438, 22)
  draw_sprite_stretched(sui_9slice, 0, 2, 50, 438, count * 12 + 11)
  draw_text(8, 32, "Player List")

  let my_id = global.steam_get_user_steam_id()
  let i = count - 1
  let all = global.rm_mp.player_peers
  while i >= 0 {
    let user_id = global.steam_lobby_get_member_id(i)
    let inst = all[user_id]
    let y = 56 + 12*i
    if user_id == my_id {
      draw_text(8, y, string(global.steam_get_persona_name()))
      draw_text(365, y, "You")
    } else if inst {
      draw_text(8, y, inst.name)
      if inst.room == 0 {
        draw_text(365, y, "Menu")
      } else {
        draw_text(365, y, global.rm_mp.character_names[inst.character])
      }
    } else {
      draw_text(8, y, " ... ")
    }
    i -= 1
  }
}

if global.component.button(
  2, 2, 96, 22, if lobby { "Invite Players" } else { " Create Lobby" }
) {
  if lobby {
    global.steam_lobby_activate_invite_overlay()
  } else {
    global.steam_lobby_create(1, 50)
  }
}

if global.component.button(
  102, 2, 86, 22, if global.rm_mp.enable_palette_packets { "[X] Palettes" } else { "[ ] Palettes"}
) {
  let enable = !global.rm_mp.enable_palette_packets
  global.rm_mp.enable_palette_packets = enable
  ini_open("mods/rmml/ghost_mp/config.ini")
  ini_write_real("ghost_mp", "enable_palette_packets", enable)
  ini_close()
}

if global.component.button(
  192, 2, 56, 22, if global.rm_mp.show_players_on_map { "[X] Map" } else { "[ ] Map"}
) {
  let enable = !global.rm_mp.show_players_on_map
  global.rm_mp.show_players_on_map = enable
  ini_open("mods/rmml/ghost_mp/config.ini")
  ini_write_real("ghost_mp", "show_players_on_map", enable)
  ini_close()
}

if global.component.button(
  250, 2, 166, 22,
  "Emote: " + global.rm_mp.key_get_name(global.rm_mp.emote_button)
) {
  menu.state = -514
}

draw_set_color(col)
